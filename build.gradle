
def checkProperty(String propName) {
    if (!project.hasProperty(propName)) return false
    String prop = project.property(propName)
    return prop != null && prop.length() > 0
}

def getPropertyOrElse(String propName, String alternative) {
    if (!checkProperty(propName)) return alternative
    return project.property(propName)
}

def isApplicationProject(String projectName) {
    return projectName.startsWith('demo') \
        && !projectName.endsWith('-api') \
        && !projectName.endsWith('-shared') \
        && !projectName.endsWith('-security')
}

def isLibraryProject(String projectName) {
    return projectName.startsWith('sbp-')
}

buildscript {
    ext.springBootVersion = '2.2.0.RELEASE'
    ext.lombokVersion = '1.18.8'
    ext.pf4jVersion = '3.1.0'

    repositories {
        jcenter()
        mavenCentral()
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath "org.yaml:snakeyaml:1.19"
//        classpath 'ch.raffael.markdown-doclet:markdown-doclet:1.4' // TODO this plugin is not worked since JDK 11
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

plugins {
    id 'idea'
    id 'nu.studer.jooq' version '3.0.3' apply false
    id 'org.flywaydb.flyway' version "5.2.4" apply false
}

//*************************************************************************
// IDEA
//*************************************************************************

idea {
    module {
        inheritOutputDirs = true
        downloadSources = true
    }
}

//*************************************************************************
// Sub Project Config
//*************************************************************************

subprojects {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
        maven { url "https://jitpack.io" }
    }

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
//    apply plugin: 'ch.raffael.markdown-doclet'
    if (isApplicationProject(name)) {
        apply plugin: 'application'
        apply plugin: 'org.springframework.boot'
        apply plugin: 'nu.studer.jooq'
        apply plugin: 'org.flywaydb.flyway'
    } else {
        apply plugin: 'maven-publish'
        apply plugin: 'signing'
    }

//*************************************************************************
// Properties
//*************************************************************************

    Properties localProp = new Properties()
    localProp.load(project.rootProject.file('local.properties').newDataInputStream())
    ext.sonatypeUsername = localProp.getProperty('sonatypeUsername')
    ext.sonatypePassword = localProp.getProperty('sonatypePassword')
    ext."signing.keyId" = localProp.getProperty('signing.keyId')
    ext."signing.password" = localProp.getProperty('signing.password')
    ext."signing.secretKeyRingFile" = localProp.getProperty('signing.secretKeyRingFile')

    task setProperties {
        doFirst {
            project.ext.executable = "$project.name"
        }
    }

//*************************************************************************
// Compile & Assemble
//*************************************************************************

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    tasks.withType(AbstractCompile) {
        options.encoding = 'UTF-8'
    }
    tasks.withType(Javadoc) {
        options.encoding = 'UTF-8'
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    jar {
        manifest.attributes provider: 'gradle'
        enabled true
        doFirst {
            archiveFileName = "$project.name-${archiveVersion.get()}.${archiveExtension.get()}"
        }
    }

    test {
        testLogging.showStandardStreams = true
        workingDir = project.rootDir
        testLogging {
            events "failed"
            exceptionFormat "short"
        }
    }

    dependencies {
        implementation "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.pf4j:pf4j:${pf4jVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.pf4j:pf4j:${pf4jVersion}"
    }

//*************************************************************************
// Build Tasks
//*************************************************************************

    task("buildApp") {
        finalizedBy jar
    }

//*************************************************************************
// Spring Boot
//*************************************************************************

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

    if (isApplicationProject(name)) {

        bootRun {
            doFirst {
                jvmArgs = ["-Dfile.encoding=UTF-8", "-Dserver.port=${http_port}"]
            }
        }

//*************************************************************************
// Application & Distribution
//*************************************************************************

        startScripts {
            dependsOn setProperties
            doFirst {
                it.setApplicationName("$executable")
                it.defaultJvmOpts.add("-Dserver.port=$http_port")
            }
            startScripts.defaultJvmOpts.add("-XX:NewSize=192m")
            startScripts.defaultJvmOpts.add("-XX:+UseParallelOldGC")
            startScripts.defaultJvmOpts.add("-XX:+UseAdaptiveSizePolicy")
            startScripts.defaultJvmOpts.add("-XX:MaxGCPauseMillis=1000")
            startScripts.defaultJvmOpts.add("-Dfile.encoding=UTF-8")
            startScripts.defaultJvmOpts.add("-Djava.security.egd=file:/dev/./urandom")

            if (checkProperty('java_max_memory')) {
                startScripts.defaultJvmOpts.add("-Xmx$java_max_memory")
            }
            if (checkProperty('java_min_memory')) {
                startScripts.defaultJvmOpts.add("-Xms$java_min_memory")
            }

            if (checkProperty("jmx_host")) {
                startScripts.defaultJvmOpts.add("-Dcom.sun.management.jmxremote")
                startScripts.defaultJvmOpts.add("-Dcom.sun.management.jmxremote.authenticate=false")
                startScripts.defaultJvmOpts.add("-Dcom.sun.management.jmxremote.ssl=false")
                startScripts.defaultJvmOpts.add("-Dcom.sun.management.jmxremote.local.only=false")
                startScripts.defaultJvmOpts.add("-Djava.rmi.server.hostname=$jmx_host")
                if (checkProperty("jmx_port")) {
                    startScripts.defaultJvmOpts.add("-Dcom.sun.management.jmxremote.port=$jmx_port")
                }
            }
        }

        if (project.hasProperty("db.url")) {

//*************************************************************************
// Database Initialization
//*************************************************************************

            task("doMigration") {
                dependsOn processResources
                doFirst {
                    println ">>>>>>>>>>> doing db_migration on ${project.'db.url'}"

                    flyway {
                        url = project.'db.url'
                        user = project.'db.user'
                        password = project.'db.password'
                        driver = project.'db.driver'
                        schemas = ["${project.'db.schema'}"]
                        baselineVersion = '0'
                        baselineOnMigrate = true
                        table = project.'db.migration.table'
                        locations = ["filesystem:$buildDir/resources/main/${project.'db.migration.location'}"]
                    }
                }
                finalizedBy flywayMigrate, flywayInfo
            }

            task("doDataMigration") {
                dependsOn processResources
                doFirst {
                    println ">>>>>>>>>>> do db_data migration on ${project.'db.url'}"

                    flyway {
                        url = project.'db.url'
                        user = project.'db.user'
                        password = project.'db.password'
                        driver = project.'db.driver'
                        schemas = ["${project.'db.schema'}"]
                        baselineOnMigrate = true
                        baselineVersion = '0'
                        table = project.'db.data.table'
                        locations = ["filesystem:$buildDir/resources/main/${project.'db.data.location'}"]
                    }

                }
                finalizedBy flywayMigrate, flywayInfo
            }

            jooq {
                sbp(sourceSets.main) {
                    jdbc {
                        url = project.'db.url'
                        user = project.'db.user'
                        password = project.'db.password'
                        driver = project.'db.driver'
                    }
                    generator {
                        name = 'org.jooq.codegen.DefaultGenerator'
                        database {
                            name = 'org.jooq.meta.postgres.PostgresDatabase'
                            inputSchema = project.'db.schema'
                            includes = '.*'
                            excludes = '_.*'
                            forcedTypes {
                                forcedType {
                                    userType = 'com.fasterxml.jackson.databind.JsonNode'
                                    binding = 'demo.sbp.shared.PostgresJsonbBinding'
                                    types = 'jsonb'
                                }
                            }
                        }
                        target {
                            packageName = project.'jooq.target.package'
                        }
                    }
                }
            }
        }

    } else if (isLibraryProject(name)) {

    //*************************************************************************
    // Maven
    //*************************************************************************

        task sourcesJar(type: Jar, dependsOn: classes) {
            archiveClassifier.set('sources')
            from sourceSets.main.allSource
        }

        task javadocJar(type: Jar, dependsOn: javadoc) {
            archiveClassifier.set('javadoc')
            from javadoc.destinationDir
        }

        artifacts {
            archives jar
            archives sourcesJar
            archives javadocJar
        }

        group = 'org.laxture'

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    groupId = project.getGroup()
                    from components.java
                    artifact sourcesJar
                    artifact javadocJar
                    pom {
                        name = project.name
                        description = 'sbp introduce plugin oriented programming to Spring Boot'
                        url = 'https://github.com/hank-cp/sbp'
                        organization {
                            name = 'org.laxture'
                            url = 'https://laxture.org'
                        }
                        issueManagement {
                            system = 'GitHub'
                            url = 'https://github.com/hank-cp/sbp/issues'
                        }
                        license {
                            name = 'Apache License 3.0'
                            url = 'https://github.com/hank-cp/sbp/blob/master/LICENSE'
                            distribution = 'repo'
                        }
                        scm {
                            url = 'https://github.com/hank-cp/sbp'
                            connection = 'scm:git:git://github.com/hank-cp/sbp.git'
                            developerConnection = 'scm:git:ssh://git@github.com:hank-cp/sbp.git'
                        }
                        developers {
                            developer {
                                name = 'Hank CP'
                                email = 'true.cp@gmail.com'
                            }
                        }
                    }
                    repositories {
                        maven {
                            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
                            def stagingRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : stagingRepoUrl
                            credentials {
                                username sonatypeUsername
                                password sonatypePassword
                            }
                        }
                    }
                }
            }
        }

        signing {
            sign publishing.publications.mavenJava
        }
    }
}